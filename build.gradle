buildscript {
    apply from: 'https://raw.githubusercontent.com/comodal/gradle-conf/master/plugins.gradle'
    dependencies {
        classpath "$axion"
        classpath "$bintray"
    }
}

project.group = 'com.fabahaba'

ext {
    bintrayOrg = ''
    bintrayRepo = 'libs'
    vcsUrl = 'https://github.com/jamespedwards42/' + project.name
    desc = 'Redis Java 8 Client & Executor'
}
apply from: 'https://raw.githubusercontent.com/comodal/gradle-conf/master/java/java.gradle'
apply from: 'https://raw.githubusercontent.com/comodal/gradle-conf/master/axion.gradle'
apply from: 'https://raw.githubusercontent.com/comodal/gradle-conf/master/comodal.gradle'
apply from: 'https://raw.githubusercontent.com/comodal/gradle-conf/master/java/bintray-maven.gradle'
apply from: 'https://raw.githubusercontent.com/comodal/gradle-conf/master/java/checks.gradle'
apply plugin: 'findbugs'

sourceCompatibility = 1.8

sourceSets {
    test {
        java {
            srcDir 'src/unit/java'
            srcDir 'src/integ/java'
        }
        resources {
            srcDir 'src/unit/resources'
            srcDir 'src/integ/resources'
        }
    }
}

test {
    outputs.upToDateWhen { false }
    maxParallelForks = 1
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showExceptions true
        showStackTraces true
        exceptionFormat "full"
    }

    systemProperty 'jedipus.redis.port', '9736'
    systemProperty 'jedipus.redis.pass', '42'
    systemProperty 'jedipus.redis.ssl.port', '6443'
    systemProperty 'jedipus.redis.ssl.truststore.jceks', 'stunnel/integ.jks'

    systemProperty 'jedipus.redis.cluster.announceip', '127.0.0.1'
    systemProperty 'jedipus.redis.cluster.startingport', '7379'
    systemProperty 'jedipus.redis.cluster.nummasters', '3'
    systemProperty 'jedipus.redis.cluster.numslaveseach', '1'
}

task startRedis(type: Exec) {
    commandLine 'docker-compose', 'up', '-d'
}

task rmRedis(type: Exec) {
    commandLine 'docker-compose', 'down'
}

task getRedisModule() {
    OutputStream moduleHeaderFile = new File('./redis/redismodule.h').newOutputStream()
    moduleHeaderFile << new URL('https://raw.githubusercontent.com/antirez/redis/unstable/src/redismodule.h').openStream()
    moduleHeaderFile.close()
}

task compileIntegModules(type: Exec) {
    String dir = System.getProperty("user.dir");
    commandLine 'docker', 'run', '--rm', '-v', "$dir/redis/:/tmp", 'comodal/alpine-gcc-make:latest', '/bin/sh', '-c', 'make -B -C /tmp/modules'
}

task makeClean(type: Exec) {
    String dir = System.getProperty("user.dir");
    commandLine 'docker', 'run', '--rm', '-v', "$dir/redis/:/tmp", 'comodal/alpine-gcc-make:latest', '/bin/sh', '-c', 'make -C /tmp/modules clean'
}

compileIntegModules.dependsOn getRedisModule
startRedis.dependsOn compileIntegModules
test.dependsOn startRedis
test.finalizedBy rmRedis
rmRedis.finalizedBy makeClean

findbugs {
    toolVersion = "+"
    effort = "max"
    reportLevel = "low"
    ignoreFailures = false
    excludeFilter = file("findbugs-exclude.xml")
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

gradle.addListener(new TestEventLogger())

class TestEventLogger implements org.gradle.api.tasks.testing.TestListener {

    public void beforeSuite(TestDescriptor suite) {
        if (suite.getClassName() != null) {
            println 'Running ' + suite.getClassName() + ' test suite.'
        }
    }

    public void afterSuite(TestDescriptor suite, TestResult result) {
    }

    public void beforeTest(TestDescriptor test) {
        println '\n' + test.getName() + ' STARTED @ ' + new Date().toTimestamp()
    }

    public void afterTest(TestDescriptor test, TestResult result) {
        println test.getName() + ' ' + result.getResultType() + ' in ' + (result.getEndTime() - result.getStartTime()) + 'ms.'
    }
}
